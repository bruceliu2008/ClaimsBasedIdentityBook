@echo off

set OS_VERSION=
set OS_MAJOR=
set OS_MINOR=
set OS_BUILD=
set IsVistaOrHigher=
set IsWin7OrHigher=
set IsWin2K3=

for /f "skip=1" %%i in ( 'wmic os get version' ) do ( 
    set OS_VERSION=%%i 
    goto:__ver_done
)
:__ver_done

for /f "delims=. tokens=1,2,3" %%i in ("%OS_VERSION%") do ( 
    set OS_MAJOR=%%i&set OS_MINOR=%%j&set OS_BUILD=%%k  
    goto :__ver_split_done
)
:__ver_split_done

if "%OS_MAJOR%" GEQ "6" (
    set IsVistaOrHigher=true
    if "%OS_MINOR%" == "1" (
        set IsWin7OrHigher=true
        goto :__ver_set_done
    )
    if "%OS_MAJOR%" GTR "6" (
        set IsWin7OrHigher=true
        goto :__ver_set_done
    )
)

if "%OS_MAJOR%" == "5" (
    if "%OS_MINOR%" == "2" (
        set IsWin2K3=true
    )
    goto :__ver_set_done
)

:__ver_set_done


echo.
echo This script will try to undo what the SamplesPreReqSetup.bat did. It includes 
echo three steps:
echo. 
echo STEP 1: It will Tear down SSL certificate registration.
echo.
echo STEP 2: If the SamplesPreReqSetup.bat has auto generated certificates using 
echo wifmakecert.exe, this step will remove those certificates from the Trusted Root
echo Certification Authority store or Trusted People store in LocalMachine. It will
echo also delete temporary certificate files generated by the SamplesPreReqSetup.bat script.
echo.
echo STEP 3: Restart the Internet Information Service (IIS).
echo.

pause

echo -----------------------------
echo --- STEP 1: Tear down SSL --- 
echo -----------------------------

setlocal enabledelayedexpansion 

@if NOT "%IsVistaOrHigher%" == "true" (

  where /q httpcfg.exe

  if NOT !ERRORLEVEL! == 0 (
    echo The script cannot find httpcfg.exe tool in the current running environment. 
    echo You can install this by going to the following link
    echo.
    echo "http://go.microsoft.com/fwlink/?LinkID=100322"
    echo.
    echo Please ensure that httpcfg.exe is installed in your PATH or the directory 
    echo this script is running from.
    echo.
    echo Please press Ctrl-C to stop this batch file 
    pause
  )
)

endlocal

set HasSSLCert=

@if "%IsVistaOrHigher%" == "true" (
   (netsh http show sslcert | findstr /C:"0.0.0.0:8082") && set HasSSLCert=true
) else (
   (httpcfg.exe query ssl | findstr /C:"0.0.0.0:8082") && set HasSSLCert=true
)

@if NOT "%HasSSLCert%" == "true" (  
  goto :endSSLCert8082
)
 
if "%IsVistaOrHigher%" == "true" (
   REM on Vista, we use netsh instead of httpcfg.exe
   netsh http delete sslcert ipport=0.0.0.0:8082
   netsh http delete urlacl url=https://+:8082/STS/mex

   %windir%\system32\inetsrv\appcmd.exe delete apppool WifSamples
   
) else (
   REM Delete server certificate and HTTP.SYS's reference to it.
   httpcfg.exe delete ssl -i 0.0.0.0:8082

   REM Delete HTTP.SYS namespace reservation for NetworkService.
   httpcfg.exe delete urlacl -url https://+:8082/STS/mex
)

:endSSLCert8082

set HasSSLCert=

@if "%IsVistaOrHigher%" == "true" (
   (netsh http show sslcert | findstr /C:"0.0.0.0:443") && set HasSSLCert=true
) else (
   (httpcfg.exe query ssl | findstr /C:"0.0.0.0:443") && set HasSSLCert=true
)

@if NOT "%HasSSLCert%" == "true" (  
  goto :endSSLCert443
)
 
if "%IsVistaOrHigher%" == "true" (
   REM on Vista, we use netsh instead of httpcfg.exe
   netsh http delete sslcert ipport=0.0.0.0:443
) else (
   REM Delete server certificate and HTTP.SYS's reference to it.
   httpcfg.exe delete ssl -i 0.0.0.0:443
)

:endSSLCert443

call "%~dp0Scripts\SetCertHashFromFile.bat" localhost

set IsIIS7=

(reg query HKLM\Software\Microsoft\InetStp /v MajorVersion | findstr /C:"0x7") && set IsIIS7=true

if "%IsIIS7%" == "true" (
   "%~dp0Scripts\IIS7Util.exe" DeleteSslCert %localhostCERTHASH%
) else (
   cscript //nologo "%~dp0Scripts\DeleteSSLCertificateIsNeeded.js" /W3SVC/1 %localhostCERTHASH%

   if %ERRORLEVEL% == 0 (
      cscript //nologo %systemdrive%\inetpub\adminscripts\adsutil.vbs delete W3SVC/1/SecureBindings

      if "%IsVistaOrHigher%" == "true" (
        cscript //nologo "%~dp0Scripts\DeleteSSLCertificate.js" /W3SVC/1 
  
      ) else (
        REM on Win2k3, we use adsutil.vbs to remove the SSL certificate
        cscript //nologo %systemdrive%\inetpub\adminscripts\adsutil.vbs delete W3SVC/1/SSLCertHash
      )
   )
)

echo ----------------------------------------------------
echo --- STEP 2: Delete the localhost/STS certificate ---
echo ----------------------------------------------------

call "%~dp0Scripts\CleanUpCert.bat" localhost root
call "%~dp0Scripts\CleanUpCert.bat" STS trustedpeople

echo -------------------------
echo --- STEP 3: Reset IIS ---
echo -------------------------

iisreset

echo Done.
pause
